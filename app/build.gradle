apply plugin: 'com.android.application'
apply from: rootProject.file('gradle/resguard.gradle')

def static getBuildTime() {
    return new Date().format("yyyyMMddHHmmss")
}

def getAppVersionName() {
    return "${versions.versionName}.${versions.versionCode}"
}

android {
    compileSdkVersion versions.compileSdkVersion

    defaultConfig {
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion
        applicationId apps.applicationId
        versionCode versions.versionCode
        versionName getAppVersionName()
        buildConfigField 'String', 'BUILD_TIME', '"' + getBuildTime() + '"'

        testInstrumentationRunner deps.androidRunner
    }

    // keystore文件
    signingConfigs {
        douban {
            storeFile file('../data/starry_douban.jks')
            storePassword 'starry123456'
            keyAlias 'douban'
            keyPassword 'douban123456'
        }
    }

    buildTypes {
        debug {
            versionNameSuffix '_debug'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue 'string', 'debug', 'true'
        }

        release {
            debuggable false
            minifyEnabled true
            signingConfig signingConfigs.douban
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue 'string', 'debug', 'false'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    configurations.all {
        // add leakcanary, conflict with dependency 'com.android.support:support-annotations'
        // 26.0.2 adn 27.1.1 differ, force used my dependency annotations version(26.0.2)
        resolutionStrategy.force deps.annotations
    }
    //android support
    implementation deps.appcompat
    implementation deps.support
    implementation deps.recyclerview
    implementation deps.design
    implementation deps.cardview
    //test
    testImplementation deps.junit
    androidTestImplementation deps.runner
    androidTestImplementation deps.espresso
    //others
    implementation deps.okhttp3
    annotationProcessor deps.butterknifeCompiler
    implementation deps.butterknife
    implementation deps.glide
    implementation deps.gson
    debugImplementation deps.leakcanaryDebug
    releaseImplementation deps.leakcanaryRelease
    debugImplementation deps.anrDebug
    releaseImplementation deps.anrRelease
    //弹性View
    implementation deps.overscrollDecor
    //module
    implementation project(':ProgressIndicator')
    implementation project(':XRecyclerView')
    implementation project(':SmartTabLayout')
    implementation project(':RxJava')
    implementation project(':Logger')
    implementation project(':Http')
}

//modify apk output  file name, format：db-versionName.versionCode-buildType.apk
android.applicationVariants.all { variant ->
    variant.outputs.all {
        def apkName = "db-${versions.versionName}.${versions.versionCode}"
        outputFileName = "${apkName}-${buildType.name}.apk"
    }
}