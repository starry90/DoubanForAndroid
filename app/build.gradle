apply plugin: 'com.android.application'
apply from: rootProject.file('gradle/resguard.gradle')

def static getBuildTime() {
    return new Date().format("yyyyMMddHHmmss")
}

def getAppVersionName() {
    return "${versions.versionName}.${versions.versionCode}"
}

android {
    compileSdkVersion versions.compileSdkVersion

    defaultConfig {
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion
        applicationId apps.applicationId
        versionCode versions.versionCode
        versionName getAppVersionName()
        buildConfigField 'String', 'BUILD_TIME', '"' + getBuildTime() + '"'
        buildConfigField 'String', 'GIT_COMMIT_TIME', '"' + gits.gitCommitTime + '"'
        buildConfigField 'String', 'GIT_BRANCH', '"' + gits.gitCommitBranch + '"'

        testInstrumentationRunner deps.androidRunner

        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi'//, 'arm64-v8a'
        }
    }

    viewBinding {
        enabled = true
    }

    // keystore文件
    signingConfigs {
        douban {
            storeFile file('../data/starry_douban.jks')
            storePassword 'starry123456'
            keyAlias 'douban'
            keyPassword 'douban123456'
        }
    }

    buildTypes {
        debug {
            versionNameSuffix '_debug'
            minifyEnabled false
            signingConfig signingConfigs.douban
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue 'string', 'debug', 'true'
        }

        release {
            debuggable false
            minifyEnabled true
            signingConfig signingConfigs.douban
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue 'string', 'debug', 'false'
        }
    }

    sourceSets {
        main.jniLibs.srcDirs = ['libs']
    }
}

//Program type already present: javax.annotation.xxx 依赖冲突解决办法
//查看依赖关系： ./gradlew app:dependencies
//以 javax.annotation:javax.annotation-api:1.3.2 冲突为例
//第一种办法简单粗暴排除所有
//configurations {
//    all*.exclude module: 'javax.annotation-api'
//}
//第二种办法在引用冲突依赖的时候排除
//dependencies{
//    testImplementation('org.robolectric:robolectric:4.4'){
//        exclude module: 'javax.annotation-api'
//    }
//}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    configurations.all {
        // add leakcanary, conflict with dependency 'com.android.support:support-annotations'
        // 26.0.2 adn 27.1.1 differ, force used my dependency annotations version(26.0.2)
        resolutionStrategy.force deps.annotations
    }
    //android support
    implementation deps.appcompat
    implementation deps.support
    implementation deps.recyclerview
    implementation deps.design
    implementation deps.cardview
    implementation deps.lifecycleExtensions
    implementation deps.constraintLayout
    implementation deps.roomRuntime
    annotationProcessor deps.roomCompiler

    //test
    testImplementation deps.junit
    androidTestImplementation deps.runner
    androidTestImplementation deps.espresso
    //others
    implementation deps.glide
    annotationProcessor deps.glideCompiler
    implementation deps.gson
    implementation deps.okhttp3
    implementation deps.agentweb
    implementation deps.jsoup
    implementation deps.slidableactivity
    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'

    // (注：从2.1.5开始要分开Bugly SDK和NDK的配置）
    implementation deps.crashreport
    implementation deps.nativecrashreport

    debugImplementation deps.leakcanaryDebug
    releaseImplementation deps.leakcanaryRelease
    debugImplementation deps.anrDebug
    releaseImplementation deps.anrRelease

    implementation 'com.makeramen:roundedimageview:2.3.0'

    implementation 'com.github.CarGuo.GSYVideoPlayer:gsyVideoPlayer-java:v6.0.3'
    //是否需要ExoPlayer模式
//    implementation 'com.github.CarGuo.GSYVideoPlayer:gsyVideoPlayer-exo_player2:v6.0.3'
    //根据你的需求ijk模式的so
//    implementation 'com.github.CarGuo.GSYVideoPlayer:gsyVideoPlayer-armv64:v6.0.3'
    implementation 'com.github.CarGuo.GSYVideoPlayer:gsyVideoPlayer-armv5:v6.0.3'

    //module
    implementation project(':ProgressIndicator')
    implementation project(':XRecyclerView')
    implementation project(':SmartTabLayout')
    implementation project(':RxJava')
    implementation project(':Logger')
    implementation project(':Http')
    implementation project(':Overscroll')
    implementation project(':CoolIndicator')
    implementation project(':ParallaxViewPager')
    implementation project(':Cropiwa')
    implementation project(':SqliteDao')
}

//modify apk output  file name, format：db-versionName.versionCode-buildType.apk
android.applicationVariants.all { variant ->
    variant.outputs.all {
        def apkName = "db-${versions.versionName}.${versions.versionCode}"
        outputFileName = "${apkName}-${buildType.name}.apk"
    }
}