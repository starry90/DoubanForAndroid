import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
//butterknife
apply plugin: 'android-apt'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        //butterknife
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

//需要在项目根目录的build.gradle 添加 apply from: 'xxx.gradle'
def androidCode = rootProject.ext.android

def static getBuildTime() {
    def formatTime = new SimpleDateFormat("yyyy/MM/dd-HH:mm:ss")
    return formatTime.format(new Date())
}

def getAppVersionName() {
    return String.format("%s-%d", rootProject.ext.android.versionName, rootProject.ext.android.versionCode)
}

android {
    compileSdkVersion androidCode.compileSdkVersion
    buildToolsVersion androidCode.buildToolsVersion

    defaultConfig {
        minSdkVersion androidCode.minSdkVersion
        targetSdkVersion androidCode.targetSdkVersion
        applicationId androidCode.applicationId
        versionCode androidCode.versionCode
        versionName getAppVersionName()
        buildConfigField "String", "BUILD_TIME", "\"" + getBuildTime() + "\""
    }

    // keysore文件
    signingConfigs {
        douban {
            storeFile file("../data/starry_douban.jks")
            storePassword "starry123456"
            keyAlias "douban"
            keyPassword "douban123456"
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "_debug"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "debug", "true"
        }

        release {
            debuggable true
            minifyEnabled true
            signingConfig signingConfigs.douban
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "debug", "false"
        }
    }
}

def libList = rootProject.ext.dependencies

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //android support
    compile libList.appcompat
    compile libList.support
    compile libList.recyclerview
    compile libList.design
    compile libList.cardview
    //others
    compile libList.okhttp3
    apt libList.butterknifeCompiler
    compile libList.butterknife
    compile libList.glide
    compile libList.gson
    compile libList.rxjava
    compile libList.rxandroid
    //module
    compile project(':ProgressIndicator')
    compile project(':XRecyclerView')
    compile project(':SmartTabLayout')
}

//modify apk output  file name, format：db-versionName-versionCode-*.apk
android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def format = "db-%s-%s-" + output.outputFile.name
        output.outputFile = new File(output.outputFile.parent, String.format(format, androidCode.versionName, androidCode.versionCode))
    }
}